MODEL:
TITLE PIANIFICAZIONE PRODUZIONE ITTICA;

SETS:
  Specie: ricavo, quota, fabb_acqua, fabb_mangime;
  Vasche: estensione, disp_acqua;
  Allocazione(Specie, Vasche): x;
ENDSETS

[RICAVO_TOT]
MAX = @SUM( Specie(i): ricavo(i) *
  @SUM( Vasche(j): X(i,j))
);

@FOR( Vasche(j): [SUP_VASCHE]
	@SUM( Specie(i): X(i, j))
		<= estensione(j)
);

@FOR( Vasche(j): [RINNOVO]
	@SUM( Specie(i): fabb_acqua(i) * x(i, j))
		<= disp_acqua(j)
);

[MANGIME]
@SUM( Specie(i): fabb_mangime(i) *
	@SUM( Vasche(j): x(i, j)))
		<= disp_mangime;

		
@FOR( Specie(i): [DOMANDA]
	@SUM( Vasche(j): x(i, j))
		<= quota(i)
);

@FOR ( Vasche(j) | j #LT# @SIZE(Vasche): [EQUITA]
  @SUM( Specie(i): x(i, j)) / estensione(j) =
  @SUM( Specie(i): x(i, j+1)) / estensione(j+1)
);

DATA:

  Specie, ricavo, quota, fabb_acqua, fabb_mangime  =
  Orata   250000   2.8     110000        875
  Spigola 200000   2.5      80000       1300
  Trota   150000   1.2      60000        700;
  
  Vasche, estensione, disp_acqua =
    1        1.3        156000
    2        2.6        273000
    3        1.4        189000;
  disp_mangime = 5500;  
   
ENDDATA

END
